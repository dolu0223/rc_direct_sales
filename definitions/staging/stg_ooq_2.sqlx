config {
  type: "table",
  schema: "staging",
  description: "Table for Sales Order Number, Sales Order Line Number amd Material Number for OOQ"
}

SELECT
  RQMT.sls_ord_nbr,
  RQMT.mtrl_nbr,
  SUM(
    (
      RQMT.open_qty * BASE_CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR
    ) / LOCAL_CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR
  ) AS OPEN_ORDER_QUANTITY_LOCAL,
  SUM(
    (
      RQMT.open_qty * BASE_CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR
    ) / GLOBAL_CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR
  ) AS OPEN_ORDER_QUANTITY_GLOBAL
FROM
  ${ref('sls_ord_rqmt')} RQMT
  LEFT JOIN ${ref('stg_conversion')} BASE_CONV ON RQMT.mtrl_nbr = BASE_CONV.MATERIAL_NUMBER
  AND RQMT.sku_uom_cd = BASE_CONV.ALTERNATE_UNIT_OF_MEASURE_CODE
  LEFT JOIN ${ref('stg_conversion')} LOCAL_CONV ON RQMT.mtrl_nbr = LOCAL_CONV.MATERIAL_NUMBER
  AND LOCAL_CONV.ALTERNATE_UNIT_OF_MEASURE_CODE = 'ESU'
  AND RQMT.sku_uom_cd = LOCAL_CONV.BASE_UNIT_OF_MEASURE_CODE
  LEFT JOIN ${ref('stg_conversion')} GLOBAL_CONV ON RQMT.mtrl_nbr = GLOBAL_CONV.MATERIAL_NUMBER
  AND GLOBAL_CONV.ALTERNATE_UNIT_OF_MEASURE_CODE = 'SSU'
  AND RQMT.sku_uom_cd = GLOBAL_CONV.BASE_UNIT_OF_MEASURE_CODE
GROUP BY
  RQMT.sls_ord_nbr,
  RQMT.mtrl_nbr